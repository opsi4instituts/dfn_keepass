Killtask "KeePass.exe"

Set $SearchPattern$ = $ProductName$
; Parameter: $SearchPattern$ Suchbegriff in Registry
Sub_search_registry3264_uninstall_keys
; Rückgabewert: $ResultList$ gefundene Einträge

Switch count ($ResultList$)
	Case "0"
		comment "No installations of " + $SearchPattern$ + " found in $RegPathUninstall$. Nothing to do"
	EndCase
	Case "1"
		comment "Found installation in registry"
		Message "Uninstalling " + $ProductName$
		Set $RegId$ = takeString(0, $ResultList$)
		
		; welcher Installer wurde verwendet? MSI oder Inno? UninstallString verrät es.
		Set $UninstallProgram$ = GetRegistryStringValue32("[" + $RegPathUninstall$ + "\" + $RegId$ + "] UninstallString")
		; Achtung 'contains' ist case-sensitive! msiexec != MsiExec
		if (contains($UninstallProgram$, "MsiExec.exe"))
			Winbatch_uninstall_msi
			Sub_check_exitcode
			Set $UninstallDir$ = $InstallDir$
		endif
		if (contains($UninstallProgram$, "unins000.exe"))
			; doppelte Anführungszeichen entfernen
			Set $UninstallProgram$ = takeString (1, splitString ( $UninstallProgram$ , '"')) 
			Winbatch_uninstall_inno 
			Sub_check_exitcode
			Set $UninstallDir$ = ExtractFilePath($UninstallProgram$)
		endif
		
		; Languagefiles etc. bleiben in InstallDir zurück, ggf. aufräumen
		; Warum UninstallDir?! Will InstallDir aus common nicht überschreiben!
		if FileExists ($UninstallDir$)
			Files_uninstall
		endif

		comment "include custom post deinstall file"
		set $CustomPostDeinstall$ = getProductProperty("custom-post-deinstall","none")
		if not ($CustomPostDeinstall$ = "none")
			if FileExists("%ScriptPath%\custom\" + $CustomPostDeinstall$)
				include_insert "%ScriptPath%\custom\" + $CustomPostDeinstall$
			endif
		endif
	EndCase
	DefaultCase
		logError "Found more than one installation! This case is not intended yet in this script! Stop now."
		isFatalError
	EndCase
EndSwitch                    

[Winbatch_uninstall_inno]
"$UninstallProgram$" /verysilent /norestart /SUPPRESSMSGBOXES /nocancel

[Winbatch_uninstall_msi]
msiexec /x $RegId$ $SilentOption$ REBOOT=ReallySuppress

[Files_uninstall]
delete -sf $UninstallDir$