; ----------------------------------------------------------------
; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/en/credits/
; ----------------------------------------------------------------

;****************************************
; SETUPTYPE      msi
;****************************************
; PRODUCTID      keepass
; SETUPFILE      KeePass-2.28.msi
;****************************************
; LICENSE        false     
; PRODUCTDESC    KeePass is a free open source password manager, which helps you to manage your passwords in a secure way. You can put all your passwords in one database, which is locked with one master key or a key file. So you only have to remember one single master password or select the key file to unlock the whole database. 

; PRODUCTNAME    KeePass 2.28
; PRODUCTSIZE    50 MB
; PRODUCTVER     2.28
;****************************************
; SETUPSCRIPT    setup.opsiscript
; UNINSSCRIPT    uninstall.opsiscript
;****************************************
; MSIID          {5621BB3B-DFF3-48CA-93FE-4A018FB593B0}
; USE-MST        
; INSTALLDIR     (not used)
;****************************************
; TIMESTAMP      11.02.2015 12:04:42
;****************************************

[Initial]
setLogLevel=6
;  Log Errors in Logfile but don't abort:
ExitOnError=True
; Show syntax errors in the script:
ScriptErrorMessages=on
; Dont trace step by step through the script:
TraceMode=off
; let started programs run in front of the winst window
StayOnTop=True


[Actions]
requiredWinstVersion >= "4.11.5.13"
ScriptErrorMessages = false

DefVar $ProductId$ 
DefVar $Setupfile$ 
DefVar $MinimumSpace$
DefVar $LogDir$
DefVar $PrettyName$ 
DefVar $Version$
DefVar $InstallDir$
DefVar $PathToExe$
DefVar $DesktopIconName$
DefVar $Property_DesktopIcon$
DefVar $CustomPostInstall$

include_insert "insert_check_exit_code.opsiinc"
include_append "section_linkfolder.opsiinc"

; ----------------------------------------------------------------
; $ProductId$ is the name of the product in opsi, only lower letters, no umlauts, no white spaces, use '-' as a seperator
Set $LogDir$            = "%SystemDrive%\tmp"
Set $ProductId$         = "dfn_keepass"
Set $PrettyName$ 			  = "KeePass"
Set $MinimumSpace$      = "50 MB"
Set $Version$	          = strPart("%installingProdVersion%", "1", calculate(strPos("%installingProdVersion%","-") + "-" + "1" ))
Set $Setupfile$         = $PrettyName$ + "-" + $Version$ + ".msi"

Set $Property_DesktopIcon$ = GetProductProperty("desktopicon","True")
Set $DesktopIconName$      = $PrettyName$
Set $InstallDir$           = "%ProgramFiles32Dir%\KeePass2x"
Set $PathToExe$            = $InstallDir$ + "\KeePass.exe"
; ----------------------------------------------------------------

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
   LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
   isFatalError "No Space"
   ; Stop process and set installation status to failed
else
	 Killtask "KeePass.exe"
   comment "Show product picture"
   ShowBitmap "%ScriptPath%\" + $ProductId$ + ".png" $PrettyName$ + " " + $Version$

   if FileExists("%ScriptPath%\delsub.opsiscript")
      Message "Uninstalling old version..."
      comment "Start uninstall sub section"
      Sub "%ScriptPath%\delsub.opsiscript"
   endif
   
   Message "Installing " + $ProductId$  + " ..."
   comment "Start setup program"
   ChangeDirectory "%SCRIPTPATH%"
   ;----------------------------------------------
   Winbatch_install_msi
   Sub_check_exitcode	

  if ($Property_DesktopIcon$ = "True")
    LinkFolder_create
  else
    LinkFolder_delete
  endif

  comment "include custom post install file"
  set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
  if not ($CustomPostInstall$ = "none")
	  if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
	  	include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
	  endif
  endif

endif


; ----------------------------------------------------------------
; install sections
; ----------------------------------------------------------------
[Winbatch_install_msi]
msiexec /i "%ScriptPath%\$Setupfile$"  /l* "$LogDir$\$ProductId$.install_log.txt" /qb! ALLUSERS=1 REBOOT=ReallySuppress

